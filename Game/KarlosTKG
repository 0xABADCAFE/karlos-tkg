; This is the launch script for the KarlosTKG mod.
;
; The included game engine only cares about the AB3: assign and the gamelink and the
; game link file has been updated to only use AB3: derived paths.
;
; In order to support low and high spec versions, we build up the AB3: assign using
; the ability to add paths to an assign.


; Try to autodetect which CPU to use
Set UseCPU 68030
C:CPU CHECK 68060 > NIL:
If $RC EQ 0
	Set UseCPU 68060
Else
	C:CPU CHECK 68040 > NIL:
	If $RC EQ 0
		Set UseCPU 68040
	Endif
Endif

; Select the engine version to use
Set TKGEngine `C:RequestChoice TITLE "AB3D2 Redux - $UseCPU" BODY "Choose Engine:*n*nRelease: Normal release, AGA/RTG.*nDeveloper: Developer build.*n*nNote: OCS/ECS systems are also supported in RTG mode." GADGETS "Release|Developer|Cancel"`

If $TKGEngine EQ 0
    Quit
Endif

; Select the system spec to use
Set TKGSpec `C:RequestChoice TITLE "AB3D 2 Redux - $UseCPU" BODY "Choose Game Specification:*n*nHigh Spec: UAE, PiStorm, 68080, 68060*nLow Spec: 68030+*n*nBy default, the engine will use fullscreen mode if*na 68040 or higher is detected.*nHigh spec mode uses more vector objects and*nincreased level geometry/detailing.*nFor 68030, 2/3 screen size and/or 1x2 pixelmode*nis recommended." GADGETS "High Spec|Low Spec|Cancel"`

If $TKGSpec EQ 0
    Quit
Endif

Stack 262144
Assign ab3: ""

If $TKGSpec EQ 1
   Assign ab3: HighSpec ADD
EndIf

If $TKGSpec EQ 2
   Assign ab3: LowSpec ADD
EndIf

If $TKGEngine EQ 1
   bin/$UseCPU/tkg
EndIf
If $TKGEngine EQ 2
   bin/$UseCPU/tkg_dev > T:tkg_debuglog.txt
   multiview T:tkg_debuglog.txt
EndIf

; Clean Up
Assign REMOVE ab3:


